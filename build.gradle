plugins {
	id 'java'
	id 'maven-publish'
	id 'idea'
	id 'eclipse'
	id 'fabric-loom' version '0.5-SNAPSHOT'
}

static def formatVersion(project, version) {
	return version;
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	group = project.maven_group

	sourceSets {
		testmod {
			compileClasspath += main.compileClasspath
			runtimeClasspath += main.runtimeClasspath
		}
	}

	dependencies {
		minecraft "com.mojang:minecraft:$project.minecraft_version"
		mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"
		modImplementation "net.fabricmc:fabric-loader:$project.loader_version"

		compileOnly "com.google.code.findbugs:jsr305:3.0.2"
	}

	configurations {
		dev
	}

	repositories {
		mavenLocal()
	}

	jar {
		classifier = "dev"
	}

	afterEvaluate {
		remapJar {
			input = file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar")
			archiveName = "${archivesBaseName}-${version}.jar"
		}

		artifacts {
			dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: jar
		}

		processResources {
			inputs.property "version", project.version

			from(sourceSets.main.resources.srcDirs) {
				include "fabric.mod.json"
				expand "version": project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude "fabric.mod.json"
			}
		}
	}
	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}
}

subprojects {

	dependencies {
		testmodCompile sourceSets.main.output
	}

	task remapMavenJar(type: Copy, dependsOn: remapJar) {
		afterEvaluate {
			from("${project.buildDir}/libs/$archivesBaseName-${version}.jar")
			into("${project.buildDir}/libs/")
			rename { String fn -> "$archivesBaseName-${version}-maven.jar" }
		}
	}

	task checkVersion {
		doFirst {
			def xml = new URL("http://maven.terraformersmc.com/com/terraformersmc/terraform-api/$archivesBaseName/maven-metadata.xml").text
			def metadata = new XmlSlurper().parseText(xml)
			def versions = metadata.versioning.versions.version*.text();
			if (versions.contains(version)) {
				throw new RuntimeException("${version} of $archivesBaseName has already been released!")
			}
		}
	}

	publish.mustRunAfter checkVersion

	publishing {
		publications {
			mavenJava(MavenPublication) {
				afterEvaluate {
					artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
						builtBy remapMavenJar
					}
				}
			}
		}

		setupRepositories(repositories)
	}

	javadoc.enabled = false
}

void setupRepositories(RepositoryHandler repositories) {
	//repositories.mavenLocal() // uncomment for testing
	def ENV = System.getenv()
	if (ENV.MAVEN_URL) {
		repositories.maven {
			url ENV.MAVEN_URL
			credentials {
				username ENV.MAVEN_USERNAME
				password ENV.MAVEN_PASSWORD
			}
		}
	}
}

dependencies {
	afterEvaluate {
		subprojects.each {
			compile project(path: "${it.name}", configuration: "dev")
			include project("${it.name}:")
		}
	}
}

task buildTerraform {
	subprojects.each {
		subprojects.each { dependsOn("${it.name}:build") }
	}
}

task publishTerraform {
	subprojects.each {
		subprojects.each { dependsOn("${it.name}:publish") }
	}
}

task publishTerraformLocally {
	subprojects.each {
		subprojects.each { dependsOn("${it.name}:publishToMavenLocal") }
	}
}
